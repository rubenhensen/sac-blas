module BlasLevel1;

use StructuresPlus: all;
use Math: all;
use Helper: all;

export all;

/*
 * drotg
 */
inline
double, double, double, double drotg(double a, double b)
{
  if(b == 0d) {
    r = a;
    z = 0d;
    c = 1d;
    s = 0d;
  } else if(a == 0d) {
    r = b;
    z = 1d;
    c = 0d;
    s = 1d;
  } else {
    sigma = tod(abs(a) > abs(b) ? sign(a) : sign(b));
    r = sigma * sqrt(a*a + b*b);
    c = a/r;
    s = b/r;
    z = abs(a) > abs(b) ? s : c == 0d ? 1d : 1d/c;
  }
  return (r, z, c, s);
}


/*
 * drotmg
 */
inline
double, double, double, double[5] drotmg(double d1, double d2, double x1, double y1)
{
  //A very literal translation from Fortran
  gam    = 4096d;
  gamsq  = 16777216d;
  rgamsq = 5.9604645e-8d;

  flag = 0d;
  h11  = 0d;
  h21  = 0d;
  h12  = 0d;
  h22  = 0d;

  if(d1 < 0d) {
    flag = -1d;
    
    d1 = 0d;
    d2 = 0d;
    x1 = 0d;
  } else if(d2*y1 < 0d) {
    flag = -2d;
  } else {
    p1 = d1*x1;
    p2 = d2*y1;
    q1 = p1*x1;
    q2 = p2*y1;

    if(abs(q1) > abs(q2)) {
        h21 = (-y1)/x1;
        h12 = p2/p1;

        u = 1d - h12*h21;
        if(u > 0d) {
            flag = 0d;
            d1 /= u;
            d2 /= u;
            x1 /= u;
        } else {
            flag = -1d;
            d1 = 0d;
            d2 = 0d;
            x1 = 0d;
        }
    } else {
        if(q2 < 0d) {
            flag = -1d;
            d1 = 0d;
            d2 = 0d;
            x1 = 0d;
        } else {
            flag = 1d;
            h11 = p1/p2;
            h22 = x1/y1;
            u = 1d + h11*h22;
            temp = d2/u;
            d2 = d1/u;
            d1 = temp;
            x1 = y1*u;
        }
    }

    if(d1 != 0d) {
        while(d1 < rgamsq || d1 > gamsq) {
            if(flag == 0d) {
                h11 = 1d;
                h22 = 1d;
                flag = -1d;
            } else {
                h21 = -1d;
                h12 = 1d;
                flag = -1d;
            }
            if(d1 < rgamsq) {
                d1 *= gam*gam;
                x1 /= gam;
                h11 /= gam;
                h12 /= gam;
            } else {
                d1 /= gam*gam;
                x1 *= gam;
                h11 *= gam;
                h12 *= gam;
            }
        }
    }

    if(d2 != 0d) {
        while(abs(d2) < rgamsq || abs(d2) > gamsq) {
            if(flag == 0d) {
                h11 = 1d;
                h22 = 1d;
                flag = -1d;
            } else {
                h21 = -1d;
                h12 = 1d;
                flag = -1d;
            }
            if(abs(d2) < rgamsq) {
                d2 *= gam*gam;
                h21 /= gam;
                h22 /= gam;
            } else {
                d2 /= gam*gam;
                h21 *= gam;
                h22 *= gam;
            }
        }
    }
  }

  return (d1, d2, x1, [flag, h11, h21, h12, h22]);
}


/*
 * drot
 */
inline
double[xn], double[yn] drot(int n, double[xn] x, int incx, double[yn] y, int incy, double c, double s) | n > 0, incx > 0, incy > 0, minArrLen(n, xn, incx), minArrLen(n, yn, incy)
{
  return (
    {iv -> c*x[iv] + s*y[(iv*incy)/incx] | [0] <= iv < [xn] step [incx] ; iv -> x[iv]},
    {iv -> c*y[iv] - s*x[(iv*incx)/incy] | [0] <= iv < [yn] step [incy] ; iv -> y[iv]}
  );
}


/*
 * drotm
 */
inline
double[xn], double[yn] drotm(int n, double[xn] x, int incx, double[yn] y, int incy, double[5] param) | n > 0, ismember(param[0], [1.0, 0.0, -1.0, -2.0]), minArrLen(n, xn, incx), minArrLen(n, yn, incy)
{
  flag = param[0];
  h11  = param[1];
  h21  = param[2];
  h12  = param[3];
  h22  = param[4];
  
  if(n < 1 || flag == -2d) {
    X_prime = x;
    Y_prime = y;
  } else if(flag < 0d) {
    X_prime = {i -> x[i]*h11 + y[(i*incy)/incx]*h12 | [0] <= i < [n*incx] step [incx] ; i -> x[i]};
    Y_prime = {i -> x[(i*incx)/incy]*h21 + y[i]*h22 | [0] <= i < [n*incy] step [incy] ; i -> y[i]};
  } else if(flag == 0d) {
    X_prime = {i -> x[i] + y[(i*incy)/incx]*h12 | [0] <= i < [n*incx] step [incx] ; i -> x[i]};
    Y_prime = {i -> x[(i*incx)/incy]*h21 + y[i] | [0] <= i < [n*incy] step [incy] ; i -> y[i]};
  } else {
    X_prime = {i ->  x[i]*h11 + y[(i*incy)/incx] | [0] <= i < [n*incx] step [incx] ; i -> x[i]};
    Y_prime = {i -> -x[(i*incx)/incy] + y[i]*h22 | [0] <= i < [n*incy] step [incy] ; i -> y[i]};
  }
  
  return (X_prime, Y_prime);
}


/*
 * dswap
 */
inline
double[xn], double[yn] dswap(int n, double[xn] x, int incx, double[yn] y, int incy) | n > 0, incx > 0, incy > 0, minArrLen(n, xn, incx), minArrLen(n, yn, incy)
{
  return (
    {iv -> y[(iv/incx)*incy] | [0] <= iv < [xn] step [incx] ; iv -> x[iv]},
    {iv -> x[(iv/incy)*incx] | [0] <= iv < [yn] step [incy] ; iv -> y[iv]}
  );
}


/*
 * dscal
 */
inline
double[xn] dscal(int n, double a, double[xn] x, int incx) | n > 0, incx > 0, minArrLen(n, xn, incx)
{
  return {iv -> a * x[iv] | [0] <= iv < [xn] step [incx] ; iv -> x[iv]};
}


/*
 * dcopy
 */
inline
double[yn] dcopy(int n, double[xn] x, int incx, double[yn] y, int incy) | n > 0, incx >= 0, incy > 0, minArrLen(n, xn, incx), minArrLen(n, yn, incy)
{
  return {iv -> x[(iv/incy)*incx] | [0] <= iv < [yn] step [incy] ; iv -> y[iv]};
}


/*
 * daxpy
 */
inline
double[yn] daxpy(int n, double a, double[xn] x, int incx, double[yn] y, int incy) | n > 0, incx >= 0, incy > 0, minArrLen(n, xn, incx), minArrLen(n, yn, incy)
{
  if(a == 0d) {
    result = y;
  } else {
    result = {iv -> a * x[(iv/incy)*incx] + y[iv] | [0] <= iv < [yn] step [incy] ; iv -> y[iv]};
  }
  return result;
}


/*
 * ddot
 */
inline
double ddot(int n, double[xn] x, int incx, double[yn] y, int incy) | n > 0, incx >= 0, incy >= 0, minArrLen(n, xn, incx), minArrLen(n, yn, incy)
{
  return sum( {iv -> x[iv*incx] * y[iv*incy] | [0] <= iv < [n]} );
}


/*
 * dsdot
 */
inline
double dsdot(int n, float[xn] x, int incx, float[yn] y, int incy) | n > 0, incx >= 0, incy >= 0, minArrLen(n, xn, incx), minArrLen(n, yn, incy)
{
  return sum( {iv -> tod(x[iv*incx]) * tod(y[iv*incy]) | [0] <= iv < [n] } );
}


/*
 * dnrm2
 */
inline
double dnrm2(int n, double[xn] x, int incx) | n > 0, incx > 0, minArrLen(n, xn, incx)
{
  return sqrt(sum( {iv -> x[iv]*x[iv] | [0] <= iv < [xn] step [incx]} ));
}


/*
 * dznrm2
 */
// inline
// double dznrm2(int n, complex[.] X, int incx) | n > 0, incx > 0
// {
//   X_prime = {i -> x[i*incx] | [0] <= i < [n] ; i -> zero() | i < [n*incx]};
//   return sqrt(sum(normSq(X_prime)));
// }


/*
 * dasum
 */
inline
double dasum(int n, double[xn] x, int incx) | n > 0, incx > 0
{
  return sum( {iv -> abs(x[iv]) | [0] <= iv < [xn] step [incx]} );
}


/*
 * idamax
 */
inline
int idamax(int n, double[xn] x, int incx) | n > 0, incx > 0, minArrLen(n, xn, incx)
{
  max_i = 0;
  max_v = 0d;
  ix = 0;
  for(i = 0; i < n; i++) {
    current_v = abs(x[ix]);
    if(current_v > max_v) {
      max_i = i;
      max_v = current_v;
    }
    ix += incx;
  }

  return max_i;
}
